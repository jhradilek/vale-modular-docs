# Verify that all IDs include the {context} attribute. This is to ensure
# the module can be used in multiple assemblies within the same document.
---
extends: script
message: "Use {context} in the section ID to enable module reuse."
level: warning
scope: raw
script: |
  text                := import("text")
  matches             := []

  r_comment_line      := text.re_compile("^(//|//[^/].*)$")
  r_delimited_block   := text.re_compile("^(\\.{4,}|-{4,}|/{4,})\\s*$")
  r_section_id        := text.re_compile("^\\[id=[\"'][^\"']+[\"']\\]\\s*$")

  in_delimited_block  := false
  start               := 0
  end                 := 0

  for line in text.split(scope, "\n") {
    start += end
    end    = len(line) + 1

    if r_delimited_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_delimited_block {
        in_delimited_block = delimiter
      } else if in_delimited_block == delimiter {
        in_delimited_block = false
      }
      continue
    }
    if in_delimited_block { continue }
    if r_comment_line.match(line) { continue }

    if r_section_id.match(line) && ! text.contains(line, "{context}") {
      matches = append(matches, {begin: start, end: start + end - 1})
    }
  }
