# Verify that each include directive is followed by an empty line.
---
extends: script
message: "Add an empty line after the include directive."
level: warning
scope: raw
script: |
  text             := import("text")
  matches          := []

  r_comment_line   := text.re_compile("^(//|//[^/].*)$")
  r_comment_block  := text.re_compile("^/{4,}\\s*$")
  r_include        := text.re_compile("^include::[^\\s\\[]+\\[[^\\[]*\\]\\s*$")

  document         := text.split(text.trim_suffix(scope, "\n"), "\n")
  in_comment_block := false
  need_empty_line  := false
  start            := 0
  end              := 0

  for line in document {
    start += end
    end   = len(line) + 1

    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }

    if r_include.match(line) {
      if need_empty_line {
        matches = append(matches, need_empty_line)
      }
      need_empty_line = {begin: start, end: start + end - 1}
    } else if need_empty_line {
      if text.trim_space(line) != "" {
        matches = append(matches, need_empty_line)
      }
      need_empty_line = false
    }
  }

  if need_empty_line {
    matches = append(matches, need_empty_line)
  }
